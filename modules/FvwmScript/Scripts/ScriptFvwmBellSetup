WindowTitle {Bell Setup}
WindowSize 360 230		# Taille
#ForeColor	{black}
#BackColor	{grey85}
#ShadowColor	{grey55}
#HilightColor	{grey100}
#Font		-adobe-helvetica-bold-r-*-*-12-*

Init
 Begin
  WarpPointer 2
  Set $pos=0
  Set $finish=1
  While $finish=={1} Do
  Begin
    Set $pos=(Add $pos 1)
    Set $tmp=(GetOutput {xset -q} $pos 1)
    If $tmp=={bell} Then
      Set $finish=0
    If $pos=={50} Then
      Set $finish=0
  End
  If $pos=={50} Then
  Begin
   Do {Exec sh -c "echo 'BellSetup: uncompatible xset'"}
   Quit
  End
  Set $SAVEDVOL=(GetOutput  {xset -q} $pos 3)
  Set $SAVEDPITCH=(GetOutput  {xset -q} $pos 6)
  Set $SAVEDDURA=(GetOutput  {xset -q} $pos 9)
  ChangeValue 4 $SAVEDVOL
  ChangeTitle 6 $SAVEDPITCH
  ChangeTitle 8 $SAVEDDURA
 End

Widget 1
Property
 Size 50 30
 Position 100 180
 Type PushButton
 Title {Apply}
Main
 Case message of
  SingleClic :
  Begin
   Do {Exec xset b } (GetValue 4) { } (GetTitle 6) { } (GetTitle 8)
   Set $SAVEDVOL=(GetValue 4)
   Set $SAVEDPITCH=(GetTitle 6)
   Set $SAVEDDURA=(GetTitle 8)
  End
End

Widget 2
Property
 Size 50 30
 Position 190 180
 Type PushButton
 Title {OK}
Main
 Case message of
  SingleClic :
  Begin
   Do {Exec xset b } (GetValue 4) { } (GetTitle 6) { } (GetTitle 8)
   WriteToFile .xinitrc-fvwm {xset b } (GetValue 4) { } (GetTitle 6) { } (GetTitle 8)
   Quit
  End
End

Widget 3
Property
 Size 50 30
 Position 280 180
 Type PushButton
 Title {Cancel}
Main
 Case message of
  SingleClic :
  Begin
   Quit
  End
End

Widget 12
Property
 Size 50 30
 Position 20 180
 Type PushButton
 Title {Beep}
Main
  Case message of
  SingleClic :
  Begin
   Set $HOP={xset b } (GetValue 4) { } (GetTitle 6) { } (GetTitle 8)
   Do {Exec } $HOP
   # wait
   Set $dum=(GetOutput $HOP 1 -1)
   Do {Beep}
   Do {Exec xset b } $SAVEDVOL { } $SAVEDPITCH { } $SAVEDDURA
  End
End

Widget 4
Property
 Size 250 1
 Position 90 30
 Type HScrollBar
 MinValue 0
 MaxValue 100
 Value 0
Main
 Case message of
  SingleClic :
  Begin
  End
End

Widget 5
Property
 Position 20 53
 Type ItemDraw
 Title {Volume:}
Main
 Case message of
  SingleClic :
  Begin
  End
End

Widget 6
Property
 Size 60 1
 Position 90 90
 Type TextField
Main
 Case message of
  SingleClic :
  Begin
  End
End

Widget 7
Property
 Position 20 95
 Type ItemDraw
 Title {Pitch(Hz):}
Main
 Case message of
  SingleClic :
  Begin
  End
End

Widget 8
Property
 Size 60 1
 Position 280 90
 Type TextField
Main
 Case message of
  SingleClic :
  Begin
  End
End

Widget 9
Property
 Position 190 95
 Type ItemDraw
 Title {Duration(ms):}
Main
 Case message of
  SingleClic :
  Begin
  End
End

Widget 10
Property
 Size 340 130
 Position 10 10
 Type Rectangle
Main
 Case message of
  SingleClic :
  Begin
  End
End

Widget 11
Property
 Position 15 3
 Type ItemDraw
 Title {Bell}
Main
 Case message of
  SingleClic :
  Begin
  End
End










