WindowTitle {Logout}
WindowSize 400 250		# Taille
WindowPosition 191 143		# Position
ForeColor	{black}
BackColor	{grey85}
ShadowColor	{grey55}
HilightColor	{grey100}
Font		-adobe-helvetica-bold-r-*-*-12-*

Init
 Begin
  Do {Exec sync &}
  WarpPointer 1
  Do {Exec cat /usr/X11R6/lib/TkDesk/sounds/metal.voc > /dev/dsp &}
  Set $ToDo = Restart
 End
 
Widget 8
Property
 Position 40 40
 Type ItemDraw
 Icon shutdown.xpm
Main
 Case message of
  SingleClic :
  Begin
  End
End

Widget 7
Property
 Size 370 220
 Position 15 15
 Type Rectangle
Main
 Case message of
  SingleClic :
  Begin
  End
End

Widget 6
Property
 Title {Are you sure to:}
 Position 100 50
 Type ItemDraw
 Value 0
 Font -adobe-helvetica-bold-r-*-*-12-*
Main
 Case message of
  SingleClic :
  Begin
  End
End

Widget 1
Property
 Title {OK}
 Size 60 28
 Position 280 190
 Type PushButton
 Value 1
Main
 Case message of
  SingleClic :
  Begin
   If $ToDo == Logout Then
    Do {Quit}
   Else
   Begin
    If $ToDo == Restart Then
    Begin
     Do {Exec sync}
     Do {Exec xterm -geometry 80x12+150+130 -bg grey85 -fg red -cr red -e pauseme /sbin/shutdown -r -f now}
     Quit
    End
    Else
    Begin
     Do {Exec sync}
     Do {Exec xterm -geometry 80x12+150+130 -bg grey85 -fg red -cr red -e pauseme /sbin/shutdown -h -f now}
     Quit
    End
   End
  End
End

Widget 2
Property
 Title {Cancel}
 Size 60 28
 Position 190 190
 Type PushButton
 Value 1
Main
 Case message of
  SingleClic :
  Begin
   Quit
  End
End

Widget 3
Property
 Title {Shut down the computer}
 Position 110 90
 Type RadioButton
 Value 0
Main
 Case message of
  SingleClic :
  Begin
   ChangeValue 4 0 
   ChangeValue 5 0 
   Set $ToDo = Halt
  End
End

Widget 4
Property
 Title {Restart the computer}
 Position 110 120
 Type RadioButton
 Value 1
Main
 Case message of
  SingleClic :
  Begin
   ChangeValue 3 0
   ChangeValue 5 0
   Set $ToDo = Restart
  End
End

Widget 5
Property
 Title {Logout}
 Position 110 150
 Type RadioButton
 Value 0
Main
 Case message of
  SingleClic :
  Begin
   ChangeValue 3 0
   ChangeValue 4 0
   Set $ToDo = Logout
  End
End
















